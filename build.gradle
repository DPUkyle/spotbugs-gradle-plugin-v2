plugins {
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'com.diffplug.gradle.spotless' version '3.26.1'
    id 'net.ltgt.errorprone' version '0.7'
    id 'se.patrikerdes.use-latest-versions' version '0.2.7'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.github.hierynomus.license' version '0.15.0'
}

version = '2.0.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/license.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/publish.gradle"

repositories { mavenCentral() }

ext {
    junitVersion = '5.5.2'
    errorproneVersion = '2.3.4'
    spotBugsVersion = '3.1.11'
    immutablesVersion = '2.8.2'
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
    compileOnly "com.github.spotbugs:spotbugs:${spotBugsVersion}"
    compileOnly "org.immutables:value:${immutablesVersion}:annotations"
    annotationProcessor "org.immutables:value:${immutablesVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test { useJUnitPlatform() }

task processVersionFile(type: WriteProperties) {
    outputFile file('src/main/resources/spotbugs-gradle-plugin.properties')

    property 'spotbugs-version', spotBugsVersion
}
tasks.processResources.dependsOn processVersionFile

defaultTasks 'licenseFormatMain', 'licenseFormatTest', 'spotlessApply', 'build'
